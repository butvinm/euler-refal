/* The MIT License (MIT)

Copyright © 2024 Mikhail Butvin


Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the “Software”), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*/


/* Generator abstraction and utils for Refal 5 Lambda.

Generator function interface:

  MyGenerator {
    () = t.InitCtx t.Next;
    t.Ctx = t.NewCtx t.Next;
    t.Ctx = Gen-Stop;
  }

Example:

  Fib {
    () = (1 1) 1;
    (8 13) = Gen-Stop;
    (s.PP s.P)
      = <Add s.PP s.P> : s.Next
      = (s.P s.Next) s.Next;
  }
*/

*$FROM LibraryEx
$EXTERN Apply;


/* Expands generator to an object expression.

Usage:
  = <Gen-Iter &Fib> : 1 1 2 3 5 8 13
  = <Gen-Iter &Fib (3 5)> : 8 13
*/
$ENTRY Gen-Iter {
  t.Gen = <Gen-Iter t.Gen ()>;

  t.Gen t.Ctx
    = <Apply t.Gen t.Ctx> : {
      Gen-Stop = ;
      t.NewCtx e.Next = (e.Next) <Gen-Iter t.Gen t.NewCtx>;
    };
}


/* Take first n values from generator.

Usage:
  = <Gen-Take 3 <Gen-Range 1 10>> : (1 )(2 )(3 )
*/
$ENTRY Gen-Take {
  t.Gen = <Gen-Take 1 t.Gen>;

  s.N t.Gen = <Gen-Take (s.N) t.Gen>;

  (e.N) t.Gen
    = {
      () = <Gen-TakeImpl (e.N ()) t.Gen>;
      (e.N^ t.Ctx) = <Gen-TakeImpl (e.N t.Ctx) t.Gen>;
    };
}


Gen-TakeImpl {
  (0 t.Ctx) t.Gen = Gen-Stop;

  (e.N t.Ctx) t.Gen
    = <Apply t.Gen t.Ctx> : {
      Gen-Stop = Gen-Stop;
      t.NewCtx e.Next = (<Sub (e.N) 1> t.NewCtx) e.Next;
    };
}


/* Auxiliary empty generator. */
$ENTRY Gen-Empty {
  = { t.Ctx = Gen-Stop };
}


/* Yields terms from e-expression. */
$ENTRY Gen-FromList {
  e.List
    = {
      () = <Gen-FromListImpl e.List>;
      ((e.List^)) = <Gen-FromListImpl e.List>;
    };
}


Gen-FromListImpl {
  /* empty */ = Gen-Stop;
  (e.H) e.T = ((e.T)) e.H;
}


/* Python-like range generator. Yields integers from s.Start to s.Stop with step s.Step.

Usage:
  * Call without arguments will generate infinite sequence of integers starting from 0
  = <Gen-Iter <Gen-Range>> : (0 )(1 )(2 )(3 )(4 )(5 )(6 )(7 )(8 )(9 )...

  * Call with start and stop values (macrodigits)
  = <Gen-Iter <Gen-Range 3 10>> : (3 )(4 )(5 )(6 )(7 )(8 )(9 )

  * Call with start, stop and step values. Start and stop is macrodigits, step is a number
  = <Gen-Iter <Gen-Range 3 10 2>> : (3 )(5 )(7 )(9 )

  * Call with start, stop and step values. All values are numbers
  = <Gen-Iter <Gen-Range (1 0) (1 10) 2>> : (1 0)(1 2)(1 4)(1 6)(1 8)
*/
$ENTRY Gen-Range {
  = <Gen-Range (0) (Inf) 1>;

  s.Start s.Stop = <Gen-Range (s.Start) (s.Stop) 1>;

  s.Start s.Stop s.Step = <Gen-Range (s.Start) (s.Stop) s.Step>;

  (e.Start) (e.Stop) e.Step
    = {
      () = <Gen-RangeImpl ((e.Start) (e.Stop) e.Step)>;
      t.Ctx = <Gen-RangeImpl t.Ctx>;
    };
}


Gen-RangeImpl {
  ((e.Start) (Inf) e.Step)
    = <Add (e.Start) e.Step> : e.Next
    = ((e.Next) (Inf) e.Step) e.Start;

  ((e.Start) (e.Stop) e.Step)
    = <Add (e.Start) e.Step> : e.Next
    = <Compare (e.Start) e.Stop> : {
      '-' = ((e.Next) (e.Stop) e.Step) e.Start;
      e._ = Gen-Stop;
    };
}


/* Yields while values satisfy predicate.

Usage:
  = <Gen-Iter
    <Gen-TakeWhile
      { e.Val = <Compare 5 e.Val> : { '+' = True; e._ = False } }
      <Gen-Range 1 10>
    >
  > : ((0 )(1 )(2 )(3 )(4 )
*/
$ENTRY Gen-TakeWhile {
  t.Pred t.Gen
    = {
      t.Ctx
        = <Apply t.Gen t.Ctx> : {
          Gen-Stop = Gen-Stop;
          t.NewCtx e.Next
            = <Apply t.Pred e.Next> : {
              False = Gen-Stop;
              True = t.NewCtx e.Next;
          };
        };
    };
}

/* Skips values while they satisfy predicate.

Usage:
  = <Gen-Iter
    <Gen-DropWhile
      { e.Val = <Compare 5 e.Val> : { '+' = True; e._ = False } }
      <Gen-Range 1 10>
    >
  > : ((5 )(6 )(7 )(8 )(9 )
*/
$ENTRY Gen-DropWhile {
  t.Pred t.Gen
    = {
      t.Ctx
        = <Apply t.Gen t.Ctx> : {
          Gen-Stop = Gen-Stop;
          t.NewCtx e.Next
            = <Apply t.Pred e.Next> : {
              False = t.NewCtx e.Next;
              True = <<Gen-DropWhile t.Pred t.Gen> t.NewCtx>;
          };
        };
    };
}


/* Yields only values satisfying predicate.

Usage:
  = <Gen-Iter
    <Gen-Filter
      { e.Val = <Mod (e.Val) 2> : { 0 = True; e._ = False } }
      <Gen-Range>
    >
  > : (5 )(6 )(7 )(8 )...
*/
$ENTRY Gen-Filter {
  t.Pred t.Gen
    = {
      t.Ctx
        = <Apply t.Gen t.Ctx> : {
          Gen-Stop = Gen-Stop;
          t.NewCtx e.Next
            = <Apply t.Pred e.Next> : {
              True = t.NewCtx e.Next;
              False = <<Gen-Filter t.Pred t.Gen> t.NewCtx>;
          };
        };
    };
}


/* Yields values from two generators sequentially.

Usage:
  = <Gen-Iter
    <Gen-Chain
      <Gen-Range 1 5>
      <Gen-Range 6 10>
      <Gen-Range 11 15>
    >
  > : (1 )(2 )(3 )(4 )(5 )(6 )(7 )(8 )(9 )(10 )(11 )(12 )(13 )(14 )(15 )
*/
$ENTRY Gen-Chain {
  = <Gen-Empty>;

  e.Gens
    = {
      ()
        = e.Gens : t.Gen e.Rest
        = <Apply t.Gen ()> : t.NewCtx e.Next
        = (t.NewCtx e.Gens) e.Next;

      (t.Ctx e.Gens^)
        = e.Gens : t.Gen e.Rest
        = <Apply t.Gen t.Ctx> : {
          Gen-Stop = <<Gen-Chain e.Rest> ()>;
          t.NewCtx e.Next = (t.NewCtx e.Gens) e.Next;
        };
    }
}


/* Map generator values with a function.

Usage:
  = <Gen-Iter
    <Gen-Map
      &Inc
      <Gen-Range 1 5>
    >
  > : (2 )(3 )(4 )(5 )(6 )
*/
$ENTRY Gen-Map {
  s.F t.Gen
    = {
      t.Ctx
        = <Apply t.Gen t.Ctx> : {
          Gen-Stop = Gen-Stop;
          t.NewCtx e.Next = t.NewCtx <s.F e.Next>;
        };
    };
}


/* Reduce generator values with a function.

Usage:
  = <Gen-Iter
    <Gen-Reduce
      &Add
      0
      <Gen-Range 1 5>
    >
*/
$ENTRY Gen-Reduce {
  s.F e.Acc t.Gen = <Gen-DoReduce s.F (e.Acc) t.Gen ()>;
}


Gen-DoReduce {
  s.F (e.Acc) t.Gen t.Ctx
    = <Apply t.Gen t.Ctx> : {
      Gen-Stop = e.Acc;
      t.NewCtx e.Next
        = <s.F (e.Acc) e.Next> : e.NewAcc
        = <Gen-DoReduce s.F (e.NewAcc) t.Gen t.NewCtx>;
    };
}


/* Compose several generators by passing values from one to another.

Each argument is a function that takes a result of the previous generator and returns a new generator.
Argument for the first function is provided by the caller.

Usage:
  = <Gen-Iter
    <Fetch
      1
      <Gen-Pipe
        { t.S = <Gen-Range t.S 3>}
        { t.A = <Gen-Map { t.B = t.A t.B} <Gen-Range <Inc t.A> 4>> }
        { t.A t.B = <Gen-Map { t.C = t.A t.B t.C} <Gen-Range <Inc t.B> 5>> }
      >
  > : (1 2 3) (1 2 4) (1 3 4) (2 3 4)
*/
$ENTRY Gen-Pipe {
  /* empty */
    = {
      e.V = {
        () = (Gen-Stop) e.V;
        (Gen-Stop) = Gen-Stop;
      }
    };

  t.GenFn e.GenFns
    = {
      e.Args
        = <Apply t.GenFn e.Args> : t.FirstGen
        = <Gen-DoPipe t.FirstGen e.GenFns>;
    };
}


/*
Dear me from the future, please refactor this function or better kill it so it won't suffer anymore.
*/
Gen-DoPipe {
  t.FirstGen = t.FirstGen;

  t.FirstGen t.GenFn e.GenFns
    = {
      ()
        = <Apply t.FirstGen ()> : {
          Gen-Stop = Gen-Stop;
          t.Ctx e.Next
            = <Apply t.GenFn e.Next> : t.FollowGen
            = <Gen-DoPipe t.FollowGen e.GenFns> : t.FollowGen^
            = <Apply t.FollowGen ()> : {
              Gen-Stop
                = <Gen-DoPipe t.FirstGen t.GenFn e.GenFns> : t.FirstGenPiped
                = <Apply t.FirstGenPiped (t.Ctx ())>;
              t.NewFollowCtx e.FollowNext = (() t.NewFollowCtx) e.FollowNext;
            };
        };

      (t.Ctx t.FollowCtx)
        = <Apply t.FirstGen t.Ctx> : {
          Gen-Stop = Gen-Stop;
          t.NewCtx e.Next
            = <Apply t.GenFn e.Next> : t.FollowGen
            = <Gen-DoPipe t.FollowGen e.GenFns> : t.FollowGen^
            = <Apply t.FollowGen t.FollowCtx> : {
              Gen-Stop
                = <Gen-DoPipe t.FirstGen t.GenFn e.GenFns> : t.FirstGenPiped
                = <Apply t.FirstGenPiped (t.NewCtx ())>;
              t.NewFollowCtx e.FollowNext = (t.Ctx t.NewFollowCtx) e.FollowNext;
            };
        };
    };
}


/* Get first item from a generator and returns new generator that starts from this value.

Effectively, split a generator into head and tail.

Usage:
  = <Gen-ChopHead <Gen-Range 0 5>> : 0 <Gen-Range 1 5>
*/
$ENTRY Gen-ChopHead {
  t.Gen
    = <Apply t.Gen ()> : {
      Gen-Stop = Gen-Stop <Gen-Empty>;

      t.Ctx e.Next
        = e.Next {
          () = <Apply t.Gen t.Ctx>;
          t.Ctx^ = <Apply t.Gen t.Ctx>;
        };
    };
}
